/*
solution:	raytracing
project:	rtengine1
file:		ps_rt0.txt
author:		cj
*/

uniform int uniNumSpheres;
uniform sampler2D uniSphereGeometrySampler;
uniform sampler2D uniSphereColorSampler;

varying vec3 varEyePos;
varying vec3 varViewDir;
varying vec3 varLightPos;

const float eps = 0.01;

// constant shading parameters
const float reflectivity = 0.3;
const float shininess = 5.0;
const vec3 specularColor = vec3(1.0, 1.0, 1.0);

struct Ray {
	vec3 origin;
	vec3 direction;
};

struct Sphere {
	vec3 center;
	float radius;
};

struct MInfo {
	float dist;
	vec3 where;
	vec3 normal;
};

bool IntersectAnalytically(Ray ray, Sphere sphere, out MInfo info) {
	float B = 2.0 * dot(ray.direction, ray.origin - sphere.center);
	vec3 d = ray.origin - sphere.center;
	float C = dot(d, d) - sphere.radius * sphere.radius;
	
	float M = B * B - 4.0 * C;
	if(0.0 > M) return false;
	float D = sqrt(M);

	float t0 = 0.5 * (-B - D);
	if(t0 > 0.0) {
		info.dist = t0;
		info.where = ray.origin + t0 * ray.direction;
		info.normal = normalize(info.where - sphere.center);
		return true;
	}

	float t1 = 0.5 * (-B + D);
	if(t1 > 0.0) {
		info.dist = t1;
		info.where = ray.origin + t1 * ray.direction;
		info.normal = normalize(info.where - sphere.center);
		return true;
	}

	return false;
}

struct WInfo {
	Ray ray;
	MInfo minfo;
	vec3 where;
	vec3 normal;
	vec3 color;
};

bool WorldIntersects(Ray ray, out WInfo info) {
	bool hit = false;
	float minDist = 0.0;
	int minIndex = 0;
	int i = 0;

	for(; i < uniNumSpheres; ++i) {
		vec4 sphereData = texelFetch(uniSphereGeometrySampler, ivec2(i, 0), 0);
		vec3 center = sphereData.rgb;
		float radius = sphereData.a;
		Sphere sphere = Sphere(center, radius);

		MInfo minfo;
		if(IntersectAnalytically(ray, sphere, minfo)) {
			float dist = minfo.dist;
			if(abs(dist) > eps) {
				if(!hit) {
					hit = true;
					minDist = dist;
					minIndex = i;
					info.minfo = minfo;
				} else if(minDist > dist) {
					minDist = dist;
					minIndex = i;
					info.minfo = minfo;
				}
			}
		}
	}

	if(hit) {
		info.ray = ray;
		info.color = vec3(texelFetch(uniSphereColorSampler, ivec2(minIndex, 0), 0));
	}

	return hit;
}

vec3 CastPrimary(Ray ray) {
	WInfo info;

	vec3 fragColor = vec3(0.0, 0.0, 0.0);
	float falloff = 1.0;

	vec3 eye = ray.origin;

	for(int depth = 0; depth < 10; ++depth) {
		bool hit = WorldIntersects(ray, info);
		vec3 where = info.minfo.where;
		vec3 normal = info.minfo.normal;

		if(hit) {
			// cast shadow ray
			vec3 dir = normalize(varLightPos - where);
			Ray shadowRay = Ray(where, dir);
			WInfo linfo;
			if(!WorldIntersects(shadowRay, linfo)) {
				// diffuse
				float d = clamp(dot(dir, normal), 0.0, 1.0);
				fragColor += falloff * info.color * d;

				// specular
				if(0.0 < d) {
					vec3 s = normalize(dir + eye - where);
					d = max(dot(s, normal), 0.0);
					fragColor += falloff * pow(d, shininess) * specularColor;
				}
			}
		} else { // miss
			fragColor += falloff * vec3(0.39215, 0.58431, 0.92941);
			break;
		}

		// prepare reflection ray
		ray = Ray(where, reflect(ray.direction, normal));
		falloff = falloff * reflectivity;
	}

	return fragColor;
}

void main() {
	Ray primary = Ray(varEyePos, normalize(varViewDir));
	vec3 fragColor = CastPrimary(primary);

	fragColor = clamp(fragColor, vec3(0.0, 0.0, 0.0), vec3(1.0, 1.0, 1.0));

	gl_FragColor = vec4(fragColor, 1.0);
}